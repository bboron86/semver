buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'

project.version = '0.1'
project.group = 'de.maibornwolff.ace'

jar {
    baseName = 'semver'
    version =  null
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile("org.springframework.cloud:spring-cloud-starter-consul-discovery")

    compile("io.springfox:springfox-swagger2:2.5.0")
    compile("io.springfox:springfox-swagger-ui:2.5.0")

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Brixton.RELEASE'
    }
}

task buildDocker(type: Docker, dependsOn: jar) {
    group = 'docker'
    applicationName = jar.baseName
    setTagVersionToLatest()
    dockerfile = file('docker/Dockerfile')
    setEnvironment('VERSION', project.version)
    doFirst {
        copy {
            from jar
            into stageDir
        }
        copy {
            from file('docker/entrypoint.sh')
            into stageDir
        }
    }
}

build.dependsOn buildDocker

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
